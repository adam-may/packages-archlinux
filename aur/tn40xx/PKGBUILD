# $Id$
# Maintainer: Ido Rosen <ido@kernel.org>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/ido/packages-archlinux
#       Otherwise, open a GitHub issue.  Thank you! -Ido
#

pkgname='tn40xx'
pkgdesc="Tehuti Networks tn40xx network driver."
pkgver='0.3.6.12.2'
pkgrel=1
arch=('x86_64')
url='http://www.tehutinetworks.net/'
license=('custom')
depends=('linux-lts')
makedepends=('linux-lts-headers')
options=('!libtool' '!strip' '!makeflags' '!buildflags' 'staticlibs')
source=("http://www.tehutinetworks.net/images/UL240756/${pkgname}-${pkgver}.tgz"
        "tn40xx.install")
sha512sums=('a9e1eb3bb0b4a2f538dc79add45e764750d30f6b271865eeda2552a7623eead84dd48cb56179bbb9982922d696fec4e00f85787cdde4c47ad863dd0f3f34fe17'
            '911a59684dd0f7fa9913546a90f6144947251f2afe9fd17f6f0b9acb08c93a8dde0df70b2d1cd58de4d60eaffc7b608a978f7f2ffd4f69a22a31319818e806c0')
install=tn40xx.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}/"
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/"

    #install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    #install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
    install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
    install -m644 Readme release_notes \
            "${pkgdir}/usr/share/doc/${pkgname}"
    install -dm755 "${pkgdir}/lib/modules/$(uname -r)/drivers/net/"
    install -m755 ${pkgname}.ko "${pkgdir}/lib/modules/$(uname -r)/drivers/net/"
    install -dm755 "${pkgdir}/etc/pm/config.d/"
    echo "SUSPEND_MODULES=${pkgname}" > "${pkgdir}/etc/pm/config.d/${pkgname}"
}
